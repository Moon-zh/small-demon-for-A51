CS1	EQU	P2.0	//数码管
CS2	EQU	P2.1
E	EQU	P2.2
DS18B20	EQU	P2.3	//18b20
SMG	EQU	P0

DS0	EQU	30H
DS1	EQU	31H
DS2	EQU	32H
DS3	EQU	33H
DS4	EQU	34H
DS5	EQU	35H
DS6	EQU	36H
DS7	EQU	37H
DS8	EQU	38H
W1	EQU	39H	//读温度中转单元
W2	EQU	3AH   
   
   
	ORG	00H
	AJMP	MAIN
	ORG	30H
	
MAIN:	MOV	SP,#80H
	MOV	R0,#DS0
	MOV	B,#8
M1:	MOV	@R0,#10
	INC	R0
	DJNZ	B,M1

MAI:	LCALL	MY		//主程序
	MOV	DPTR,#TW1
	LCALL	ZHCX
	MOV	DS7,A
	MOV	DS6,B
	LCALL	MY
	MOV	DPTR,#TW2
	LCALL	ZHCX
	MOV	DS4,A
	MOV	DS3,B
	LCALL	MY
	MOV	DPTR,#TW3
	LCALL	ZHCX
	MOV	DS1,A
	MOV	DS0,B
	AJMP	MAI

CWCX:	LCALL	BI1		//匹配序列号
	LCALL	CS18B20
	MOV	A,#55H
	LCALL	E18B20
	PUSH	DPH
	PUSH	DPL
CW1:	CLR	A
	MOVC	A,@A+DPTR
	CJNE	A,#0FFH,CW0
	AJMP	CWC
CW0:	LCALL	E18B20
	INC	DPTR
	AJMP	CW1

CWC:	POP	DPL
	POP	DPH
	RET

ZHCX:	LCALL	CWCX		//匹配序列号		
	MOV	A,#44H		//温度转换
	LCALL	E18B20
	LCALL	CWCX		//匹配序列号
	MOV	A,#0BEH		//读温度命令
	LCALL	E18B20
	
	LCALL	RD18B20		//读温度转换
	SWAP	A
	ANL	A,#0FH
	MOV	W2,A
	LCALL	RD18B20
	SWAP	A	
	ADD	A,W2
	MOV	W2,A

	MOV	A,W2
	MOV	B,#10
	DIV	AB
	RET	

CS18B20:CLR	DS18B20		//18b20初始化
	MOV	R0,#240
	DJNZ	R0,$
	SETB	DS18B20
	MOV	R0,#30
	DJNZ	R0,$
	JNB	DS18B20,$
	MOV	R0,#250
	DJNZ	R0,$
	RET

E18B20:	MOV	B,#08H		//写数据
E1:	CLR	DS18B20
	RRC	A
	MOV	DS18B20,C
	MOV	R0,#28
	DJNZ	R0,$
	SETB	DS18B20
	DJNZ	B,E1
	RET

RD18B20:MOV	B,#08H		//读数据
RD1:	CLR	DS18B20
	MOV	R0,#1
	DJNZ	R0,$
	SETB	DS18B20
	MOV	C,DS18B20
	RRC	A
	MOV	R0,#23
	DJNZ	R0,$
	DJNZ	B,RD1
	RET

BI1:	LCALL	CS18B20		//跳过读码
	MOV	A,#0CCH
	LCALL	E18B20
	RET

MY:	MOV	DPTR,#TAB	//数码显示
	MOV	R1,#DS0
	MOV	R2,#0FEH
MA:	MOV	A,@R1
	MOVC	A,@A+DPTR
	MOV	SMG,A
	INC	R1
	CLR	E
	CLR	CS1
	SETB	E
	SETB	CS1
	MOV	A,R2
	MOV	SMG,A
	RL	A
	MOV	R2,A
	CLR	E
	CLR	CS2
	SETB	E
	SETB	CS2
	LCALL	SMYS
	LCALL	SMQP
	CJNE	R1,#38H,MA
	RET
	
SMYS:	MOV	R7,#0FFH
	DJNZ	R7,$
	RET

SMQP:	MOV	SMG,#0FFH
	CLR	E
	CLR	CS1
	SETB	E
	SETB	CS1
	CLR	E
	CLR	CS2
	SETB	E
	SETB	CS2
	RET
	
TAB:DB 0C0H,0F9H,0A4H,0B0H,99H,92H,82H,0F8H,80H,90H,0FFH
   
TW1:	DB	28H,66H,9FH,90H,02H,00,00H,19H,0FFH//室温//18b20 序列号			  
TW2:	DB	28H,14H,0CEH,80H,02H,00,00H,8FH,0FFH//高温

TW3:	DB 	28H,0F1H,65H,90H,02H,00,00H,36H,0FFH//低温
    
   	END